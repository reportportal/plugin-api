plugins {
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'java-library'
}

project.ext {
    releaseMode = project.hasProperty("releaseMode") ? project.releaseMode.toBoolean() : false
    publishRepo = "https://maven.pkg.github.com/reportportal/plugin-api"
    dependencyRepos = ["commons-model", "commons-rules", "commons-dao", "commons-bom"]
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-commons.gradle'
apply from: scriptsUrl + '/signing.gradle'
//apply from: scriptsUrl + '/build-quality.gradle'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.epam.reportportal:commons-bom:5.14.1')
    }
}
ext['spring-boot.version'] = "${springBootVersion}"

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:commons-dao'
        api 'com.epam.reportportal:commons'
    } else {
        implementation 'com.github.reportportal:commons-dao:a815c8b'
        api 'com.github.reportportal:commons:613c82b'
    }

    api 'org.pf4j:pf4j:3.12.1'
    api 'org.pf4j:pf4j-update:2.3.0'

    implementation 'org.hibernate:hibernate-core:6.6.5.Final'

    testImplementation "org.junit.jupiter:junit-jupiter-api"
}


wrapper {
    gradleVersion = '8.12'
}

tasks.preTagCommit.enabled = false
tasks.updateVersion.enabled = false
tasks.commitNewVersion.enabled = false
