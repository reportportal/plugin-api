plugins {
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    publishRepo = "https://maven.pkg.github.com/reportportal/plugin-api"
    dependencyRepos = ["commons-model", "commons-rules", "commons-dao", "commons-bom"]
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-commons.gradle'
apply from: scriptsUrl + '/signing.gradle'
apply from: scriptsUrl + '/build-quality.gradle'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
wrapper.gradleVersion('6.0')

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }
    if (releaseMode) {
        dependencyRepos.forEach { path ->
            maven {
                setUrl("https://maven.pkg.github.com/reportportal/${path}")
                credentials {
                    username = findProperty("githubUserName")
                    password = findProperty("githubToken")
                }
            }
        }
    } else {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.github.reportportal:commons-bom:7affe20')
    }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:commons-model'
        compile 'com.epam.reportportal:commons-rules'
        compile 'com.epam.reportportal:commons-dao'
    } else {
        compile 'com.github.reportportal:commons-dao:59bed64'
        compile 'com.github.reportportal:commons-model:444356b'
        compile 'com.github.reportportal:commons-rules:2fe7a4b'
    }
    compile 'org.pf4j:pf4j:2.4.0'

    testCompile 'junit:junit'
}
