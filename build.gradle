plugins {
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'java-library'
}

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    publishRepo = "https://maven.pkg.github.com/reportportal/plugin-api"
    dependencyRepos = ["commons-model", "commons-rules", "commons-dao", "commons-bom"]
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-commons.gradle'
apply from: scriptsUrl + '/signing.gradle'
//apply from: scriptsUrl + '/build-quality.gradle' // TODO: fix gradle scripts

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext['spring-boot.version'] = "${springBootVersion}"

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:5.12.0' : 'com.epam.reportportal:commons-bom:5.12.0')
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:commons-dao'
        api 'com.epam.reportportal:commons'
    } else {
        implementation 'com.github.reportportal:commons-dao:1fb643b'
        api 'com.github.reportportal:commons:bbb9a1a'
    }

    api 'org.pf4j:pf4j:3.12.1'
    api 'org.pf4j:pf4j-update:2.3.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api"
}
