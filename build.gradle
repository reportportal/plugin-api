plugins {
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'java-library'
}

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    publishRepo = "https://maven.pkg.github.com/reportportal/plugin-api"
    dependencyRepos = ["commons-model", "commons-rules", "commons-dao", "commons-bom"]
}

def scriptsUrl = 'https://raw.githubusercontent.com/reportportal/gradle-scripts/' +
        (releaseMode ? getProperty('scripts.version') : 'develop')

apply from: scriptsUrl + '/release-commons.gradle'
apply from: scriptsUrl + '/signing.gradle'
//apply from: scriptsUrl + '/build-quality.gradle' // TODO: fix gradle scripts

repositories {
    mavenCentral { url "https://repo1.maven.org/maven2" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
}

ext['spring-boot.version'] = '3.3.4' // TODO: set spring-boot version in commons-bom

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:5.12.1' : 'com.epam.reportportal:commons-bom:5.12.1')
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:commons'
        implementation 'com.epam.reportportal:commons-dao'
    } else {
        implementation 'com.github.reportportal:commons-dao:653ca3f'
        api 'com.github.reportportal:commons:develop-SNAPSHOT'
    }
    implementation 'com.fasterxml.jackson:jackson-bom:2.18.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    api 'org.hibernate.validator:hibernate-validator:6.2.5.Final'

    api 'org.pf4j:pf4j:3.12.1'
    api 'org.pf4j:pf4j-update:2.3.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
